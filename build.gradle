import java.util.regex.Matcher
import java.util.regex.Pattern

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id "com.android.application" version "8.0.2" apply false
    id "com.android.library" version "8.0.2" apply false
    id "org.jetbrains.kotlin.android" version '1.9.0' apply false
    id "org.jetbrains.kotlin.plugin.serialization" version "1.9.0"
    id "com.google.gms.google-services" version "4.3.15" apply false
}

ext {
    environment = getCurrentEnvironment()
    if(environment.isEmpty()){
        environment = "dev"
    }

    config = getProps('config/'+environment+'.properties')
}

def getCurrentEnvironment() {
    Gradle gradle  = getGradle()

    def pattern = Pattern.compile("([A-Z][A-Za-z]+)(Release|Debug)")
    def environment = ""

    gradle.getStartParameter().getTaskNames().any { name ->
        Matcher matcher = pattern.matcher(name)
        if(matcher.find()) {
            environment = matcher.group(1).toLowerCase()
            return true
        }
    }

    return environment
}

def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}